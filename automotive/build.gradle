/*
SPDX-FileCopyrightText: 2021 MoleMan1024 <moleman1024dev@gmail.com>
SPDX-License-Identifier: GPL-3.0-or-later
*/
plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "jacoco"
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        manifestPlaceholders
        applicationId "de.moleman1024.audiowagon"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 201
        versionName "2.0.1"
        resValue "string", "VERSION_NAME", versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
        manifestPlaceholders["requestLegacyExternalStorage"] = "false"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders["requestLegacyExternalStorage"] = "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        emulatorSDCard {
            initWith debug
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        // for testing Android Room database migrations
        getByName("androidTest").assets.srcDir("$projectDir/schemas")
    }

    packagingOptions {
        exclude "META-INF/spring.factories"
        exclude "META-INF/spring.handlers"
        exclude "META-INF/spring.schemas"
        exclude "META-INF/spring.tooling"
        exclude "META-INF/license.txt"
        exclude "META-INF/notice.txt"
    }
}

dependencies {
    def androidx_test_version = "1.4.0"
    def kotlinx_serialization_version = "1.3.0"
    def mockito_version = "3.11.1"
    def room_version = "2.4.2"

    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    implementation files("libs/libaums-0.8.2.aar")
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.2.11"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.1"

    // junit4 for local unit tests
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"

    // junit4 for instrumented tests on device/emulator
    debugImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    debugImplementation "androidx.fragment:fragment-testing:1.4.1"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "org.springframework:spring-context:5.3.8"
    androidTestImplementation "org.springframework:spring-test:5.3.8"
    androidTestImplementation "androidx.room:room-testing:2.4.2"
    // in-memory filesystem to easily create different mocked USB drives in testcases
    androidTestImplementation "com.google.jimfs:jimfs:1.2"
    androidTestImplementation files("libs/mp3agic-0.9.2-SNAPSHOT.jar")
}
